apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID // Group Id of your Maven Central Repository
                artifactId PUBLISH_ARTIFACT_ID // Replace with your desired artifact ID
                version PUBLISH_VERSION// Replace with the version of your SDK

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                pom {
                    name = PUBLISH_ARTIFACT_ID // SDK Name: Replace with your SDK's name
                    description = 'The Yoverify Android SDK is a software development kit (SDK) designed to provide developers with a seamless integration of Yoverify\'s verification services into their Android applications. Yoverify is a platform that offers various identity verification and authentication solutions, and the Android SDK enables developers to easily incorporate these features into their mobile apps.' // Replace with your SDK's description
                    url = 'https://github.com/YouverifyHQ/youverify-android-sdk' // Project URL: Replace with your repository URL

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'YouverifyHQ' // Replace with your username
                            name = 'Famous Ehichioya' // Replace with your name
                            email = 'developer@youverify.co' // Replace with your email
                        }
                    }

                    scm {
                        url = 'https://github.com/YouverifyHQ/youverify-android-sdk.git' // SCM URL: Replace with your repository URL
                        connection = 'scm:git:github.com/YouverifyHQ/youverify-android-sdk.git'
                        developerConnection = 'scm:git:ssh://github.com/YouverifyHQ/youverify-android-sdk.git'
                    }
                }
            }
        }
    }
}